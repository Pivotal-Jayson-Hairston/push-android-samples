// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
        classpath 'com.google.gms:google-services:3.0.0'
    }
}

def getSdkDirFromLocalProperties() {
    def rootDir = project.rootDir
    def localProperties = new File(rootDir, "local.properties")
    Properties properties = new Properties()
    localProperties.withInputStream { instr ->
        properties.load(instr)
    }
    properties.getProperty('sdk.dir')
}

def androidHome = System.env['ANDROID_HOME']

def androidHomeIsDefined = androidHome?.trim()

def sdkDir = androidHomeIsDefined ? androidHome : getSdkDirFromLocalProperties()

subprojects {

    println "SDK dir: $sdkDir"

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "file://$sdkDir/extras/android/m2repository" }
        maven { url "file://$sdkDir/extras/google/m2repository" }
    }

    ext {
        // Pivotal CF Mobile Services dependencies
        pushSdkVersionName = System.getenv("PUSH_SDK_VERSION") ?: "1.7.0"
        pushSdk = 'io.pivotal.android:push:' + pushSdkVersionName

        println "Using Push SDK version: " + pushSdkVersionName

        debugKeystorePath = System.getenv("PUSH_DEBUG_SIGNING_KEY") ?: new File(System.getProperty('user.home'), '.android').absolutePath + "/PCFPush.android.debug.keystore"
        println "Using debugKeystorePath: " + debugKeystorePath
    }
}
